apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'

def groupIdDefined = "top.hasiy"
def artifactIdDefined = "spinkitprogressbar"
def versionDefined = "1.3.0"

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 130
        versionName "1.3.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}
repositories {
    mavenCentral()
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'top.hasiy:Android-SpinKit:1.0.1'
}

android.libraryVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "${artifactIdDefined}-${versionDefined}.aar"
    }
}

task createPom {
    apply plugin: 'maven'
    description "create Pom for spinkitprogressbar"
    doLast {
        pom {
            project {
                groupId "${groupIdDefined}"
                artifactId "${artifactIdDefined}"
                version "${versionDefined}"
                packaging "aar"
                licenses {
                    license {
                        name 'GNU Lesser General Public License version 3'
                        url 'https://opensource.org/licenses/lgpl-3.0.html'
                    }
                }
            }
        }.withXml {
            //一定要有不然  依赖会缺失
            def dependenciesNode = asNode().appendNode('dependencies')
            configurations.implementation.allDependencies.each { dependency ->
                def dependencyNode = dependenciesNode.appendNode('dependency')
                dependencyNode.appendNode('groupId', dependency.group)
                dependencyNode.appendNode('artifactId', dependency.name)
                dependencyNode.appendNode('version', dependency.version)
            }
        }.writeTo("${artifactIdDefined}-${versionDefined}.pom")
    }
}

// 生成Java源码jar文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveFileName = "${artifactIdDefined}-${versionDefined}-${"sources"}.jar"
}

// 生成Javadoc文件
task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
    options.encoding = "utf-8"
    options.charSet = "utf-8"
}

afterEvaluate {
    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompileProvider.get().classpath.files
    })
}

// 生成Javadoc_jar文件
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveFileName = "${artifactIdDefined}-${versionDefined}-${"javadoc"}.jar"
    from javadoc.destinationDir
}
